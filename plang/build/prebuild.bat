:: This is the pre-build event batch file. Everything in it will be performed
:: before the actual build.

set BuildMode=%1

set BuildNumFile=source\buildnum.h
set BuildNumConstant=PLANG_BUILD_NUM

if %BuildMode%==RELEASE (
    call :IncrementBuildNum
)

goto :eof

:: Increment build number. The file 'buildnum.h' is included in common.h.
:: -----------------------------------------------
:IncrementBuildNum
    set /p DefBuildNum=<%BuildNumFile%
    for /f "tokens=3" %%a in ("%DefBuildNum%") do set BuildNum=%%a
    set /a BuildNum+=1
    set DefBuildNum=#define %BuildNumConstant% %BuildNum%
    (echo %DefBuildNum%)>%BuildNumFile%
    (echo // DO NOT MODIFY! GENERATED BY BUILD/PREBUILD.BAT)>>%BuildNumFile%
    exit /b
:: -----------------------------------------------
